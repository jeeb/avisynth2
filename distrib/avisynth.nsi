; Generated NSIS script file (generated by makensitemplate.phtml 0.21)
; by 130.225.167.75 on Sep 25 02 @ 09:39

; NOTE: this .NSI script is designed for NSIS v1.8+
BrandingText "Avisynth 2 installer."
Name "AviSynth 2.08"
OutFile "AviSynth208.exe"
Icon AviSynth.ico
EnabledBitmap "on.bmp"
DisabledBitmap "off.bmp"

; Some default compiler settings (uncomment and change at will):
SetCompress auto ; (can be off or force)
SetDatablockOptimize on ; (can be off)
CRCCheck on ; (can be off)
AutoCloseWindow false ; (can be true for the window go away automatically at end)
ShowInstDetails show ; (can be show to have them shown, or nevershow to disable)
SetDateSave on ; (can be on to have files restored to their orginal date)

LicenseText "You must agree to this license before installing."
LicenseData "gpl.txt"

InstallDir "$PROGRAMFILES\AviSynth2"
InstallDirRegKey HKEY_LOCAL_MACHINE "SOFTWARE\AviSynth" ""
DirShow show ; (make this hide to not let the user change it)
DirText "Select the directory to install documentation in:"
ComponentText "AviSynth Installation."

;Function .onInit
;FunctionEnd

;0
Section "AviSynth Base (required)" ; (default section)
SetShellVarContext All
ClearErrors
SetOutPath "$SYSDIR"
File "..\release\avisynth.dll"

IfErrors dll_not_ok

; Write GPL
SetOutPath "$INSTDIR"
File "gpl.txt"


ReadRegStr $0 HKEY_LOCAL_MACHINE "SOFTWARE\AviSynth" "plugindir"
StrCmp "$0" "" No_Plugin_exists Plugin_exists
No_Plugin_exists:
CreateDirectory "$INSTDIR\plugins"
StrCpy $0 "$INSTDIR\plugins"
Plugin_exists:

ClearErrors

; add files / whatever that need to be installed here.
WriteRegStr HKEY_LOCAL_MACHINE "SOFTWARE\AviSynth" "plugindir" "$0"
WriteRegStr HKEY_LOCAL_MACHINE "SOFTWARE\AviSynth" "" "$INSTDIR"
WriteRegStr HKEY_LOCAL_MACHINE "Software\Microsoft\Windows\CurrentVersion\Uninstall\AviSynth2" "DisplayName" "AviSynth 2 (remove only)"
WriteRegStr HKEY_LOCAL_MACHINE "Software\Microsoft\Windows\CurrentVersion\Uninstall\AviSynth2" "UninstallString" '"$INSTDIR\uninst.exe"'
WriteRegStr HKEY_CLASSES_ROOT "avifile\Extensions\AVS" "" "{E6D6B700-124D-11D4-86F3-DB80AFD98778}"
WriteRegStr HKEY_CLASSES_ROOT "Media Type\Extensions\.avs" "Source Filter" "{D3588AB0-0781-11CE-B03A-0020AF0BA770}"
WriteRegStr HKEY_CLASSES_ROOT "CLSID\{E6D6B700-124D-11D4-86F3-DB80AFD98778}" "" "Avisynth"
WriteRegStr HKEY_CLASSES_ROOT "CLSID\{E6D6B700-124D-11D4-86F3-DB80AFD98778}\InProcServer32" "" "avisynth.dll"
WriteRegStr HKEY_CLASSES_ROOT "CLSID\{E6D6B700-124D-11D4-86F3-DB80AFD98778}\InProcServer32" "ThreadingModel" "Apartment"

WriteRegStr HKCR ".avs" "" "avsfile"
WriteRegStr HKCR "avsfile" "" "AviSynth Script"
WriteRegStr HKCR "avsfile\DefaultIcon" "" $SYSDIR\AviSynth.dll,0

IfErrors reg_not_ok


; Create links
CreateDirectory "$SMPROGRAMS\AviSynth 2"
CreateShortCut "$SMPROGRAMS\AviSynth 2\Plugin Directory.lnk" "$0"
CreateShortCut "$SMPROGRAMS\AviSynth 2\Uninstall AviSynth.lnk" "$INSTDIR\uninst.exe"
CreateShortCut "$SMPROGRAMS\AviSynth 2\License.lnk" "$INSTDIR\gpl.txt"
; write out uninstaller
WriteUninstaller "$INSTDIR\uninst.exe"

goto dll_ok
dll_not_ok:
MessageBox MB_OK "Could not copy avisynth.dll to system directory - Close down all applications that use Avisynth, and sure to have write permission to the system directory, and try again."
Abort
dll_ok:

goto reg_ok
reg_not_ok:
MessageBox MB_OK "Could not register filter properly - Be sure to have administrator rights, and try again."
Abort
reg_ok:
SectionEnd ; end of default section

;1
SectionDivider

;2
Section "Documentation (recommended)"
SetShellVarContext All
  CreateDirectory "$INSTDIR\docs"
  SetOutPath "$INSTDIR\docs"
	File "..\docs\*.html"
  SetOutPath "$INSTDIR\docs\filters"
	File "..\docs\filters\*.html"
  CreateDirectory "$INSTDIR\Examples"
  SetOutPath "$INSTDIR\examples"
	File "Examples\*.avs"
  CreateShortCut "$SMPROGRAMS\AviSynth 2\Example Scripts.lnk" "$INSTDIR\examples"
  CreateShortCut "$SMPROGRAMS\AviSynth 2\Avisynth Documentation.lnk" "$INSTDIR\docs\index.html"
  CreateShortCut "$SMPROGRAMS\AviSynth 2\Avisynth Online.lnk" "http://avisynth.org"
SectionEnd


;3
Section "German Documentation"
SetShellVarContext All
  CreateDirectory "$INSTDIR\docs_ger"
  SetOutPath "$INSTDIR\docs_ger"
	File "..\docs_ger\*.html"
  SetOutPath "$INSTDIR\docs_ger\filters"
	File "..\docs_ger\filters\*.html"
  CreateDirectory "$INSTDIR\Examples"
  SetOutPath "$INSTDIR\examples"
	File "Examples\*.avs"
  CreateShortCut "$SMPROGRAMS\AviSynth 2\Skript Beispiele.lnk" "$INSTDIR\examples"
  CreateShortCut "$SMPROGRAMS\AviSynth 2\Deutsche Avisynth Dokumentation.lnk" "$INSTDIR\docs_ger\index.html"
  CreateShortCut "$SMPROGRAMS\AviSynth 2\Avisynth Online.lnk" "http://avisynth.org"
SectionEnd

SectionDivider

Section "Associate AVS files with Notepad"
SetShellVarContext All
  SectionIn 1 2
  WriteRegStr HKCR "avsfile\shell\open\command" "" 'notepad.exe "%1"'
SectionEnd

Section "Associate AVS files with Media Player 6.4"
;  SectionIn 1 2
  SetShellVarContext All
  IfFileExists "$PROGRAMFILES\Windows Media Player\mplayer2.exe" mplayer_found
  MessageBox MB_OK "The location of mplayer2.exe could not be found - you need to set the association manually!" IDOK mplayer_end
  goto mplayer_end
mplayer_found:	
  WriteRegStr HKCR "avsfile\shell\open\command" "" '$PROGRAMFILES\Windows Media Player\mplayer2.exe "%1"'
mplayer_end:
SectionEnd

!define SECTION_ON 0x80000000
!define SECTION_OFF 0x7FFFFFFF
!define ass_notepad 5
!define ass_mediaplayer 6

Function .onInit
  MessageBox MB_YESNO|MB_ICONQUESTION   "This will install AviSynth 2.0.8 beta. Do you wish to continue ?" IDYES NoAbort
		Abort
	NoAbort:
  SectionGetFlags ${ass_notepad} $6
  SectionGetFlags ${ass_mediaplayer} $7
  IntOp $6 $6 | ${SECTION_ON}
  IntOp $7 $7 & ${SECTION_OFF}
  SectionSetFlags ${ass_notepad} $6
  SectionSetFlags ${ass_mediaplayer} $7
  SectionSetFlags 3 0
FunctionEnd


Function .onSelChange
  SectionGetFlags ${ass_notepad} $1
  SectionGetFlags ${ass_mediaplayer} $2
  ; Set last unchecked as checked, if both are active
  IntOp $3 $1 & ${SECTION_ON}
  IntOp $4 $2 & ${SECTION_ON}
  IntOp $5 $3 & $4
  IntCmp $5 0 endosl_end   ; Both are not checked - jump!
  IntOp $6 $6 ^ ${SECTION_ON}  ; Invert on flag
  IntOp $7 $7 ^ ${SECTION_ON}
  IntOp $1 $1 & ${SECTION_OFF}  ; Clear selection
  IntOp $2 $2 & ${SECTION_OFF}
  IntOp $1 $1 | $6  
  IntOp $2 $2 | $7
  SectionSetFlags ${ass_notepad} $1
  SectionSetFlags ${ass_mediaplayer} $2
  
endosl_end:
  SectionGetFlags ${ass_notepad} $6
  SectionGetFlags ${ass_mediaplayer} $7
  IntOp $6 $6 & ${SECTION_ON}
  IntOp $7 $7 & ${SECTION_ON}
FunctionEnd


; begin uninstall settings/section
UninstallText "This will remove Avisynth from your system"

Section Uninstall
; add delete commands to delete whatever files/registry keys/etc you installed here.
MessageBox MB_YESNO "Do you want to remove pointer to plugin directory (no files will be removed)?" IDYES removeplug IDNO dontremoveplug
removeplug:
DeleteRegKey HKEY_LOCAL_MACHINE "SOFTWARE\AviSynth" 
dontremoveplug:

Delete "$INSTDIR\uninst.exe"
DeleteRegKey HKEY_LOCAL_MACHINE "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\AviSynth 2"
DeleteRegKey HKEY_CLASSES_ROOT "avifile\Extensions\AVS" 
DeleteRegKey HKEY_CLASSES_ROOT "Media Type\Extensions\.avs" 
DeleteRegKey HKEY_CLASSES_ROOT "CLSID\{E6D6B700-124D-11D4-86F3-DB80AFD98778}" 
DeleteRegKey HKEY_CLASSES_ROOT "CLSID\{E6D6B700-124D-11D4-86F3-DB80AFD98778}\InProcServer32"
DeleteRegKey HKEY_CLASSES_ROOT "CLSID\{E6D6B700-124D-11D4-86F3-DB80AFD98778}\InProcServer32"
;UnRegDLL "$SYSDIR\avisynth.dll"
Delete "$SYSDIR\avisynth.dll"
Delete "$INSTDIR\gpl.txt"
Delete "$INSTDIR\docs\filters\*.*"
Delete "$INSTDIR\docs\*.*"
Delete "$INSTDIR\docs_ger\filters\*.*"
Delete "$INSTDIR\docs_ger\*.*"
Delete "$INSTDIR\examples\audio.avs"
Delete "$INSTDIR\examples\editing.avs"
Delete "$INSTDIR\examples\processing.avs"
Delete "$INSTDIR\examples\syntax.avs"
Delete "$INSTDIR\examples\version.avs"
Delete "$SMPROGRAMS\AviSynth 2\*.*"

RMDir "$INSTDIR\docs\filters"
RMDir "$INSTDIR\docs"
RMDir "$INSTDIR\plugins"
RMDir "$INSTDIR\examples"
RMDir "$INSTDIR"
RMDir "$SMPROGRAMS\AviSynth 2"
SectionEnd ; end of uninstall section

; eof
